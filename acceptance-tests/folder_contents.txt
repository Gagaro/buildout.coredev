*** Settings ***
Resource  plone.txt

Suite setup  Log in  admin  admin

Test setup  Create Pages

Test teardown  Remove Pages

*** Test cases ***

Reorder Folder Contents
    Go to homepage
    Click Contents In Edit Bar

    # Moves the test2 page above the test1 page
    Should be above  css=tr#folder-contents-item-test1  css=tr#folder-contents-item-test2
    Reorder Element  folder-contents-item-test1  folder-contents-item-test2
    Should be above  css=tr#folder-contents-item-test2  css=tr#folder-contents-item-test1

    # Moves the test4 page above the test2 page
    Reorder Element  folder-contents-item-test4  folder-contents-item-test3
    Reorder Element  folder-contents-item-test4  folder-contents-item-test1
    Reorder Element  folder-contents-item-test4  folder-contents-item-test2

    # Moves the test3 page above the test2 page
    Reorder Element  folder-contents-item-test3  folder-contents-item-test1
    Reorder Element  folder-contents-item-test3  folder-contents-item-test2

    # The new order is 4 > 3 > 2 > 1
    Should be above  css=tr#folder-contents-item-test4  css=tr#folder-contents-item-test3
    Should be above  css=tr#folder-contents-item-test3  css=tr#folder-contents-item-test2
    Should be above  css=tr#folder-contents-item-test2  css=tr#folder-contents-item-test1

*** Keywords ***

Create Pages
    Go to homepage
    Add Page  test1
    Go to homepage
    Add Page  test2
    Go to homepage
    Add Page  test3
    Go to homepage
    Add Page  test4
    Go to homepage

Remove Pages
    Remove Content  test1
    Remove Content  test2
    Remove Content  test3
    Remove Content  test4

Reorder Element
    [arguments]  ${element}  ${destination}

    Mouse Down  css=#${element} td.draggable
    Mouse Move  css=#${destination} td.draggable
    Mouse Up  css=#${element} td.draggable
    Mouse Out  css=#${element} td.draggable

Copy ${element}
    Select Checkbox  css=#cb_${element}
	Click Button  Copy  don't wait
	Wait Until Page Contains  Paste
	Click Button  Paste  don't wait
	Wait Until Page Contains  css=#folder-contents-item-copy_of_${element}


