Repository: Products.CMFPlone
Branch: refs/heads/master
Date: 2015-06-05T00:54:20-05:00
Author: vangheem (vangheem) <vangheem@gmail.com>
Commit: https://github.com/plone/Products.CMFPlone/commit/be5f6d688fe10f100288e68b996bfc0326592880

add a few robot tests for social media metadata

Files changed:
A Products/CMFPlone/tests/robot/test_controlpanel_social.robot

diff --git a/Products/CMFPlone/tests/robot/test_controlpanel_social.robot b/Products/CMFPlone/tests/robot/test_controlpanel_social.robot
new file mode 100644
index 0000000..ab4d273
--- /dev/null
+++ b/Products/CMFPlone/tests/robot/test_controlpanel_social.robot
@@ -0,0 +1,68 @@
+*** Settings *****************************************************************
+
+Resource  plone/app/robotframework/keywords.robot
+Resource  plone/app/robotframework/saucelabs.robot
+
+Library  Remote  ${PLONE_URL}/RobotRemote
+
+Resource  keywords.robot
+
+Test Setup  Open SauceLabs test browser
+Test Teardown  Run keywords  Report test status  Close all browsers
+
+
+*** Test Cases ***************************************************************
+
+Scenario: Social settings are provided
+  Given a logged-in site administrator
+    and the social control panel
+   When I provide social settings
+   Then social tags should exist
+
+Scenario: Social tags are disabled
+  Given a logged-in site administrator
+    and the social control panel
+   When I provide social settings
+   When I disable social
+   Then social tags should not exist
+
+
+*** Keywords *****************************************************************
+
+# --- GIVEN ------------------------------------------------------------------
+
+a logged-in site administrator
+  Enable autologin as  Site Administrator
+
+the social control panel
+  Go to  ${PLONE_URL}/@@social-controlpanel
+
+
+# --- WHEN -------------------------------------------------------------------
+
+I disable social
+  UnSelect Checkbox  form.widgets.share_social_data:list
+  Click Button  Save
+  Wait until page contains  Changes saved
+
+I provide social settings
+  Input Text  name=form.widgets.twitter_username  plonecms
+  Input Text  name=form.widgets.facebook_app_id  123456
+  Input Text  name=form.widgets.facebook_username  plonecms
+  Click Button  Save
+  Wait until page contains  Changes saved
+
+
+# --- THEN -------------------------------------------------------------------
+
+social tags should exist
+  Go to  ${PLONE_URL}
+  Page should contain element  css=meta[name="twitter:site"]
+  Page should contain element  css=meta[property="og:article:publisher"]
+  Page should contain element  css=meta[property="fb:app_id"]
+
+social tags should not exist
+  Go to  ${PLONE_URL}
+  Page should not contain element  css=meta[name="twitter:site"]
+  Page should not contain element  css=meta[property="og:article:publisher"]
+  Page should not contain element  css=meta[property="fb:app_id"]


